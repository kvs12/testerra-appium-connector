ext {
    doNotPublish = this.&dnp

    // Minimum required Testerra version
    testerraCompileVersion = '2-SNAPSHOT'
    // Unit tests use the latest Testerra version
    testerraTestVersion = '[2.0-RC,3-SNAPSHOT)'
    moduleVersion = '2-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }
}

subprojects {

    group 'eu.tsystems.mms.tic.testerra'
    version moduleVersion

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    // important!
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    test {
        useTestNG()
    }
}

// Add `doNotPublish(this)` to module's build.gradle if no publishing is needed
def dnp(prj) {
    prj.jar.enabled = true
    prj.publish.enabled = false
    prj.publishToMavenLocal.enabled = false
    println("Do not publish JAR from " + prj)
}

// Do not move this integration because `group` and `version` is needed for publishing
apply from: rootProject.file('publish.gradle')
